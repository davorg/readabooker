#!/usr/bin/env perl

use strict;
use warnings;

use builtin qw[trim];
use feature 'say';

use OpenAPI::Client::OpenAI;
use Time::Piece;
use Encode qw[encode];

use Booker::Schema;

my $sch = Booker::Schema->get_schema;

my $count = 0;
my $books = $sch->resultset('Book');

while ($count < 20 and my $book = $books->next) {
  next if defined $book->blurb;
  ++$count;
  my $blurb = describe_title($book);
  $book->update({ blurb => $blurb });
}

sub describe_title {
  my ($book) = @_;

  my ($title, $author) = ($book->title, $book->author->name);

  my $debug = 1;

  my $api_key = $ENV{"OPENAI_API_KEY"} or die "OPENAI_API_KEY is not set\n";

  my $client = OpenAPI::Client::OpenAI->new;

  my $prompt = join " ",
    'Produce a 100-200 word description for the book',
    "'$title' by $author",
    'Do not mention the fact that the book was short-listed for (or won)',
    'the Booker Prize';

  my $res = $client->createChatCompletion({
    body => {
      model => 'gpt-4o',
      # model => 'gpt-4.1-nano',
      messages => [
          { role => 'system', content => 'You are someone who knows a lot about popular literature.' },
          { role => 'user', content => $prompt },
      ],
      temperature => 1.0,
    },
  });

  my $text = $res->res->json->{choices}[0]{message}{content};
  $text = encode('UTF-8', $text);

  say $text if $debug;

  return $text;
}

